# P10k 即时提示(必须在最顶部)
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi


# --- 插件相关前置配置 ---
# ez-compinit配置: 开启补全系统缓存
# 路径优先级 $ZSH_COMPDUMP > $XDG_CACHE_HOME/zsh/zcompdump > ~/.cache/zsh/zcompdump 
zstyle ':plugin:ez-compinit' 'use-cache' 'yes'

# ez-compinit配置: 使用oh-my-zsh 风格的补全
# 官方提供 styles: gremlin, ohmy, prez, zshzoo
# 只有 ohmy 预设与 fzf-tab 兼容, 其它模式 group 标题会有无法渲染的占位符
zstyle ':plugin:ez-compinit' 'compstyle' 'ohmy'

# 配合 fzf-tab 相关补全选项
zstyle ':completion:*:git-checkout:*' sort false   # git-checkout 关闭按字母排序
zstyle ':completion:*:descriptions' format '[%d]'  # 窗口增加 group 标题

# fzf-tab 内部配置
zstyle ':fzf-tab:*' fzf-flags --bind=tab:accept                               # 使用 Tab 键确认 fzf-tab 中的选项
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath' # 在 cd 界面使用 eza 显示目录内容


# --- 加载插件 ---
source ${ZDOTDIR:-~}/zsh_plugins.zsh


# 启动 P10k 主题
[[ ! -f ${ZDOTDIR:-~}/p10k.zsh ]] || source ${ZDOTDIR:-~}/p10k.zsh


# 初始化 zoxide
if (( $+commands[zoxide] )); then
  eval "$(zoxide init zsh)"
fi

# 启动fzf-tab
enable-fzf-tab

# --- 快捷键绑定 ---

# 配置 zsh-history-substring-search 快捷键
bindkey '^[[A'   history-substring-search-up    2>/dev/null # '^[[A' 序列的上方向键
bindkey '^[OA'   history-substring-search-up    2>/dev/null # '^[[A' 序列的上方向键

bindkey '^[[B'   history-substring-search-down  2>/dev/null # '^[[B' 序列的下方向键
bindkey '^[OB'   history-substring-search-down  2>/dev/null # '^[OB' 序列的下方向键


# --- 用户函数 ---
# 代理开启和关闭函数
proxy_on() {
  # proxy_on 支持默认参数
  # proxy_on 127.0.0.1 7890
  local PROXY_HOST="${1:-127.0.0.1}"
  local PROXY_PORT="${2:-7897}"

  export http_proxy="http://${PROXY_HOST}:${PROXY_PORT}"
  export https_proxy="http://${PROXY_HOST}:${PROXY_PORT}"
  export all_proxy="socks5://${PROXY_HOST}:${PROXY_PORT}"
  export no_proxy="localhost,127.0.0.1,::1,10.0.0.0/8,192.168.0.0/16"

  echo "Proxy ON → ${PROXY_HOST}:${PROXY_PORT}"
}
proxy_off() {
  unset http_proxy https_proxy all_proxy no_proxy
}


# --- 环境变量 ---
# 配置默认编辑器, hx > vi
export EDITOR=${${commands[hx]:+hx}:-vi}
export VISUAL=$EDITOR